WHITESPACE = _{ " " | "\t" }

selector = { SOI ~ path_segment+ ~ stage* ~ EOI }

path_segment = { slash ~ segment ~ predicate* }

slash = { "//" | "/" }

segment = { wildcard | ident }

predicate = { "[" ~ field ~ operator ~ value ~ "]" }

field = { json_field | ident }

json_field = { "json$" ~ ("." ~ ident)+ }

operator = { "<=" | ">=" | "<" | ">" | "=" }

wildcard = { "+" | "#" }

ident = { (ASCII_ALPHANUMERIC | "_" | "-")+ }

number = { ASCII_DIGIT+ }

boolean = { "true" | "false" }

string = { "\"" ~ (!"\"" ~ ANY)* ~ "\"" }

value = { boolean | number | string }

stage = { pipe ~ function }
pipe = _{ "|>" }
function = { ident ~ "(" ~ func_arg? ~ ")" }
func_arg = _{ duration | field }
duration = { number ~ "s" }
